plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'dev.lukebemish.docpatcher'
version = '0.2.5'

java {
    // I don't feel like dealing with old java...
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        name = "NeoForged"
        url = "https://maven.neoforged.net/releases/"
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.0.1'
    implementation 'net.neoforged.javadoctor:injector-spoon:2.0.5'
    implementation 'fr.inria.gforge.spoon:spoon-core:10.4.1'
    implementation 'com.google.guava:guava:32.1.2-jre'
    implementation 'org.apache.commons:commons-text:1.10.0'
}

processResources {
    from rootProject.file("LICENSE")
}

gradlePlugin {
    website = 'https://github.com/lukebemishprojects/DocPatcher'
    vcsUrl = 'https://github.com/lukebemishprojects/DocPatcher.git'
    plugins {
        docPatcher {
            id = 'dev.lukebemish.docpatcher'
            displayName = 'DocPatcher'
            description = 'Generates and applies javadoc patches to source files using javadoctor and spoon'
            implementationClass = 'dev.lukebemish.docpatcher.plugin.impl.DocPatcherPlugin'
            tags.addAll(['javadoc', 'patch'])
        }
    }
}

if (System.getenv('GRADLE_PLUGIN_KEY')) {
    ext['gradle.publish.key'] = System.getenv('GRADLE_PLUGIN_KEY')
    ext['gradle.publish.secret'] = System.getenv('GRADLE_PLUGIN_SECRET')
}
